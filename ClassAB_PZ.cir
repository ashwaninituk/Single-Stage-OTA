Class AB Amplifier Circuit.

*******************************************************
* AC Analysis of Class AB Amplifier                   *                                                     
* Assignment : Course Project                         *
* Deadline:15/Apr/2021 E.O.D.                         *
* Author: Ashwani Kumar Sharma                        *
* Roll No.: 204102402                                 *
*******************************************************

.include 45nm_LP.pm

*Circuit Netlist and Supplies.
Vdd sup 0 dc 1.1V
Vin_n in1 0 dc 0.35V ac -0.01mV
Vin_p in2 0 dc 0.35V ac 0.01mV
M1 d1 in1 s1 s1 pmos W=21.6u L=270n
M2 d2 in2 s2 s2 pmos W=21.6u L=270n
M7 d7 g78 sup sup pmos W=21.6u L=270n
M8 d8 g78 sup sup pmos W=21.6u L=270n
M7c g78 g78c d7 d7 pmos W=21.6u L=270n
M8c out g78c d8 d8 pmos W=21.6u L=270n
M9c g78 g910c d1 d1 nmos W=5.4u L=270n
M10c out g910c d2 d2 nmos W=5.4u L=270n
M9 d1 g9 0 0 nmos W=10.8u L=270n
M10 d2 g10 0 0 nmos W=10.8u L=270n
Cbat1 g5 g10 0.7pf
Cbat2 g6 g9 0.7pf
Cb1 g10 0 0.175pf
Cb2 g9 0 0.175pf
CL out 0 70pF

*Adaptive Biasing Circuit for folding MOS
Ibias1 sup g11 10u
M11c g11 g11c s11 s11 nmos W=5.4u L=270n
M11 s11 g11 0 0 nmos W=5.4u L=270n
Mr1 g10 g11 g11 g11 pmos W=2u L=270n
Mr2 g9 g11 g11 g11 pmos W=2u L=270n

*Adaptive Biasing Circuit for input MOS
Ibias2 g5 0 10u
Ibias3 g6 0 10u
M5 s2 g5 sup sup pmos W=3u L=270u
M6 s1 g6 sup sup pmos W=3u L=270u
M3 g5 in1 s2 s2 pmos W=21.6u L=270u
M4 g6 in2 s1 s1 pmos W=21.6u L=270u

*Additional Current Biasing Circuits
Mbp1 dbp1 gbp1 sup sup pmos W=21.6u L=270n
Mbp2 gbp1 g78c dbp1 dbp1 pmos W=21.6u L=270n
R1 gbp1 g78c 10k
Iref2 g78c 0 10u

Iref1 sup g910c 20u
R2 g910c gbn1 5k
Mbn2 gbn1 g910c dbn1 dbn1 nmos W=10.8u L=270n
Mbn1 dbn1 gbn1 0 0 nmos W=10.8u L=270n

Iref3 sup g11c 10u
R3 g11c gbn3 10k
Mbn4 gbn3 g11c dbn3 dbn3 nmos W=5.4u L=270n
Mbn3 dbn3 gbn3 0 0 nmos W=5.4u L=270n

*Control block starts here.
.control
  run
  pz in1 in2 out 0 vol pz
  print all
.endc
*Control block ends here.
.end